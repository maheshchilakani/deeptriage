.create-or-alter function with (folder = "AutoTriageDeepLearning", docstring = "Function to filter the data which suits for deep learning", skipvalidation = "false")
GetICMTableDataForDL(startTime:datetime, endTime:datetime)  
{
let OwningTenantNames = dynamic(['Common Data Service (CDS)', 'Admin Developer and ISV Experiences (ADIx)', 'CAP-PowerApps'
                        , 'Dynamics 365 Enterprise Service']);
cluster('icmcluster.kusto.windows.net').database('IcMDataWarehouse').Incidents
| where ModifiedDate between (startTime..endTime)
| where SourceCreatedBy == "CAPSRE Anomaly Detection Service" 
| where OwningTenantName in (OwningTenantNames)
| where Status in ('RESOLVED','MITIGATED')
| summarize arg_max(Lens_IngestionTime,*) by IcmId = tostring(IncidentId)
| project IcmId, stackTrace = tostring(split(substring(extract("<br>StackTrace:<br>.*",0,Summary),19),"<br>")[0]),Summary, Team = OwningTeamName, OwningTenantName
| where stackTrace != ""
| where stackTrace contains "Microsoft" or stackTrace contains "System"
| distinct stackTrace, Team
| extend filteredStackTrace = replace(@'[.]',@' ',stackTrace) //to remove dots
| extend filteredStackTrace = replace(@"\(.*?\)",@"",filteredStackTrace) // to remove function arguments within ()
| extend filteredStackTrace = replace(@"\<.*?\>",@"",filteredStackTrace) // to remove words between <>
| extend filteredStackTrace = replace(@"[_|<|>|`|(|)|\\|,|[|]|_|:|&|;|!|@|#|$|%|^|-|{|}|'|.]",@" ",filteredStackTrace) // to remove few special characters
| extend filteredStackTrace = replace(@"]",@" ",filteredStackTrace) // to remove ]
| extend filteredStackTrace = replace(@"[[:digit:]]+",@"  ",filteredStackTrace) // to remove numbers
| extend filteredStackTrace = replace(@"[A-Z]",@" \0",filteredStackTrace) // to insert space before every capital letter. Hence to separate words
| extend filteredStackTrace = replace(@" [A-Z|a-z] ",@" ",filteredStackTrace) // remove single letter words
| extend filteredStackTrace = replace(@" [A-Z|a-z] ",@" ",filteredStackTrace) // remove single letter words
| extend filteredStackTrace = replace(@" [A-Z|a-z] ",@" ",filteredStackTrace) // remove single letter words
| extend filteredStackTrace = replace(@" [A-Z|a-z] ",@" ",filteredStackTrace) // remove single letter words
| extend filteredStackTrace = replace(@" [A-Z|a-z]$",@" ",filteredStackTrace) // remove single letter words
| extend filteredStackTrace = replace(@" ([A-Z][A-Z]|[a-z][a-z]|[A-Z][a-z]|[a-z][A-Z]) ",@" ",filteredStackTrace) // remove two letters words
| project Team, filteredStackTrace, RawStackTrace = stackTrace
}
